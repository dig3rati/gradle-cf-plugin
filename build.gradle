/*
 * Copyright 2012 the original author or authors.
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'

repositories {
    mavenCentral()
    mavenRepo url:'http://maven.springframework.org/milestone/'
}

dependencies {
    compile gradleApi()
    compile 'org.cloudfoundry:cloudfoundry-client-lib:0.7.1'
    groovy group: 'org.codehaus.groovy', name: 'groovy', version: '1.8.6'
    testCompile 'junit:junit:4.8.2'
}

group = 'org.gradle.api.plugins'
version = '0.1.0-SNAPSHOT'

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar
    archives javadocJar
}

def credentials = [userName: System.getProperty('deployUser'), password: System.getProperty('deployPass')]

uploadArchives {
    repositories {
        mavenDeployer {
            repository(id:'sonatype-nexus-staging',url: uri('../melix-mvn-repo/releases'), authentication: credentials)
            snapshotRepository(id:'sonatype-nexus-snapshots', url: uri('https://oss.sonatype.org/content/repositories/snapshots'), authentication: credentials)
            pom.project {
                name 'CloudFoundry plugin for Gradle'
                parent {
                    groupId 'org.sonatype.oss'
                    artifactId 'oss-parent'
                    version '7'
                }
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                scm(
                        url:'git@github.com:melix/gradle-cf-plugin.git',
                        connection:'scm:git:git@github.com:melix/gradle-cf-plugin.git',
                        developerConnection:'scm:git:git@github.com:melix/gradle-cf-plugin.git')
                developers {
                    developer(id:'melix', name:'CÃ©dric Champeau')
                }
                description('A plugin for Gradle that allows various operations on the CloudFoundry platform, like deploying, managing services, ...')
            }
        }
    }
}